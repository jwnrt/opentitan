# Copyright lowRISC contributors (OpenTitan project).
# Licensed under the Apache License, Version 2.0, see LICENSE for details.
# SPDX-License-Identifier: Apache-2.0

module(name = "lowrisc_opentitan")

bazel_dep(name = "abseil-cpp", version = "20230802.0")
bazel_dep(name = "bazel_skylib", version = "1.5.0")
bazel_dep(name = "buildifier_prebuilt", version = "6.4.0")
bazel_dep(name = "googletest", version = "1.11.0")
bazel_dep(name = "lowrisc_misc_linters")
bazel_dep(name = "platforms", version = "0.0.10")
bazel_dep(name = "rules_cc", version = "0.0.9")
bazel_dep(name = "rules_foreign_cc", version = "0.9.0")
bazel_dep(name = "rules_fuzzing", version = "0.5.2")
bazel_dep(name = "rules_pkg", version = "0.7.0")
bazel_dep(name = "rules_rust", version = "0.47.1")
bazel_dep(name = "rules_shellcheck", version = "0.3.3")
bazel_dep(name = "toolchains_llvm", version = "1.0.0")

git_override(
    module_name = "lowrisc_misc_linters",
    commit = "aed701007ebc25df45e50f161eca73d37739df45",
    remote = "https://github.com/lowRISC/misc-linters",
)

git_override(
    module_name = "rules_rust",
    commit = "5b4111da6393a7639a182c011a9fad92c451891a",
    remote = "https://github.com/bazelbuild/rules_rust",
)

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2021",
    extra_target_triples = [
        "riscv32imc-unknown-none-elf",
    ],
    versions = [
        "1.71.1",
        "nightly/2023-07-30",
    ],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:docs_bzlmod.bzl", "crate")
crate.from_cargo(
    name = "crate_index",
    cargo_lockfile = "//third_party/rust:Cargo.lock",
    manifests = [
        "//third_party/rust:Cargo.toml",
    ],
)
crate.annotation(
    crate = "libudev-sys",
    patch_args = ["-p1"],
    patches = ["@//third_party/rust/patches:libudev-sys-0.1.4.patch"],
)
crate.annotation(
    crate = "mdbook",
    gen_binaries = ["mdbook"],
    patch_args = ["-p1"],
    patches = ["@//third_party/rust/patches:mdbook-landing-page-links.patch"],
)
crate.annotation(
    crate = "cryptoki",
    patch_args = ["-p2"],
    patches = [
        "@//third_party/rust/patches:cryptoki-vendor-defined-mechanism-type.patch",
    ],
)
crate.annotation(
    additive_build_file_content = """
        filegroup(
            name = "binding_srcs",
            srcs = [
                "src/lib.rs",
                "src/bindings/x86_64-unknown-linux-gnu.rs",
            ],
        )
    """,
    crate = "cryptoki-sys",
    extra_aliased_targets = {
        "cryptoki-sys-binding-srcs": "binding_srcs",
    },
)
crate.annotation(
    build_script_env = {
        "PKG_CONFIG_PATH": "$(OPENSSL_PKG_CONFIG_PATH)",
        "OPENSSL_STATIC": "1",
    },
    build_script_toolchains = ["@//third_party/rust:openssl_pkg_config_path"],
    crate = "openssl-sys",
)
use_repo(crate, "crate_index")

register_toolchains("//third_party/rust:bindgen_toolchain")

# LLVM toolchain used by bindgen (not for device software).
llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm")
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_versions = {
        "": "10.0.0",
    },
)
use_repo(llvm, "llvm_toolchain_llvm")
